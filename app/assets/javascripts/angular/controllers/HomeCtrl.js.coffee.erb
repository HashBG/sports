@hashbg_sports.controller 'HomeCtrl', ['$scope', '$location', '$http', '$modal', 'flash', 'Auth', ($scope, $location, $http, $modal,flash,Auth) ->
  if window.location.hash == ""
    $location.path('').replace()
  
  # angular-flash has the following levels: success, info, warn, error
  if window.gon.flash?
    for level, message of window.gon.flash
      if level == "alert"
        flash["error"] = message
      else if level == "notice"
        flash["success"] = message
      else
        flash["info"] = message
  
  if window.gon.current_user?
    Auth._currentUser = window.gon.current_user
    $scope.login_display = window.gon.current_user.email
  
  $scope.isAuthenticated = Auth.isAuthenticated

  $scope.selectedBets = {}
  $scope.oddsRepresentations = ["decimal", "uk", "us"]
  $scope.oddsRepresentation = "decimal"
  
  $scope.moreBetsCount = (match) ->
    i = 0
    otherBets = $scope.filterKeys(match)
    for bet in otherBets
      for k, v of match[bet]
        # k can be modifier or result
        if v.decimal
          i += 1
          break
        else
          i += 1
    i
    
  $scope.isSpecialKey = (key) ->
    (["_id", "_rev", "Full1X2", "$$hashKey"].indexOf(key) >= 0)
    
  $scope.filterKeys = (match) ->
    keys = []
    for k of match
      if ! $scope.isSpecialKey(k)
        keys.push(k)
    keys  
  
  $scope.showTickets = () ->
    Object.keys($scope.selectedBets).length > 0
    
  $scope.isBetSelected = (match, betType, betResult, modifier) ->
    m = $scope.selectedBets[match["_id"]]
    m? && m.betType == betType && m.betResult == betResult && m.modifier == modifier
  
  $scope.hasOdds = (match, betType, betResult, modifier) ->
    if modifier?
      r = match[betType][modifier][betResult]
    else
      r = match[betType][betResult]
    r?
  
  $scope.disableSetBet = (match, betType, betResult, modifier) ->
    if $scope.hasOdds(match, betType, betResult, modifier)
      m = $scope.selectedBets[match["_id"]]
      m? && ! $scope.isBetSelected(match, betType, betResult, modifier)
    else
      true
    
  $scope.removeBet = (ticketId) ->
    delete $scope.selectedBets[ticketId]
  
  $scope.setBet = (match, betType, betResult, modifier) ->
    if $scope.isBetSelected(match, betType, betResult, modifier)
      $scope.removeBet match["_id"]
    else
      if modifier?
        coefficient = match[betType][modifier][betResult]
      else
        coefficient = match[betType][betResult]
    
      $scope.selectedBets[match["_id"]] = {
        betType: betType, betResult: betResult, 
        modifier: modifier, coefficient: coefficient}
  
  $scope.printTicket = (ticket) ->
    r = ""
    r += ticket.betType
    if ticket.modifier?
      r += "/" + ticket.modifier
    r += " to " +  ticket.betResult
    r
  
  $scope.printMatchDate = (match) ->
    match.split("/")[0]

  $scope.printMatch = (match, which) ->
    d = match.split("/")
    if which?
      d[which]
    else
      d[1] + " v " + d[2]
  
  $scope.printCoefficient = (matchKey, ticket) ->
    ticket.coefficient[$scope.oddsRepresentation]
  
  normalize = (rational) ->
    # greatest common divisor
    a = Math.abs(rational[0])
    b = Math.abs(rational[1])
    while (b != 0) 
      tmp = a
      a = b
      b = tmp % b
 
    numerator = rational[0] / a
    denominator = rational[1] / a
    
    [numerator, denominator];

  $scope.printTicketCoefficientTotal = () ->
    ticketIds = Object.keys($scope.selectedBets)
    if ticketIds.length == 0
      return ""
    else if ticketIds.length == 1
      return $scope.selectedBets[ticketIds[0]].coefficient[$scope.oddsRepresentation]

    start = ticketIds.shift()
    s = parseFloat($scope.selectedBets[start].coefficient["decimal"])
    for ticketId in ticketIds 
      ticket = $scope.selectedBets[ticketId]
      s = s * parseFloat(ticket.coefficient["decimal"])
    s = Math.round(s * 10000) / 10000
    
    if $scope.oddsRepresentation == "decimal"
      s.toString()
    else
      s = normalize [(s-1)*10000, 10000]
      
      if $scope.oddsRepresentation == "uk"
        s[0].toString() + "/" + s[1].toString()
      else
        rational = s[0] / s[1]
        if rational < 1
          Math.round(-(100.0 / rational)).toString()
        else if rational > 1
          "+" + (Math.round(100 * rational).toString())
        else
          "+100"
  
  $scope.showCoefficient = (match, betType, betResult, modifier) ->
    if modifier?
      r = match[betType][modifier][betResult]
    else
      r = match[betType][betResult]
    
    if r?
      r[$scope.oddsRepresentation]
    else
      "no data"

  $scope.switchOddsRepresentation = (representation) ->
    $scope.oddsRepresentation = representation
    
  $scope.makeTheBid = () ->
    modalInstance = $modal.open
      templateUrl: "<%= asset_path('dialogs/make_bid.html') %>"
      controller: 'BetCtrl'
      resolve:
        selectedBets: ->
          $scope.selectedBets
    
  $scope.logout = ->
    flash.clean()
    Auth.logout().then((user) ->
      flash["success"] = "You have been logged out."
    , (error) ->
      flash["error"] = "There was an error logging out: " + error
    )
    
  $scope.loginDialog = () ->
    modalInstance = $modal.open
      templateUrl: "<%= asset_path('dialogs/login.html') %>"
      controller: 'SessionsCtrl'
  
  $scope.copyToClipboard = (text) ->
    window.prompt("Copy to clipboard: Ctrl+C, Enter", text);

  #$scope.$on "devise:unauthorized", (event, xhr, deferred) ->
  #  if $scope.login_display
  #    $scope.loginDialog()
  #  else
  
]

