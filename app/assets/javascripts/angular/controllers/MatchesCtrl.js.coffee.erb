@hashbg_sports.controller 'MatchesCtrl', ['$scope', '$location', '$http', '$routeParams', '$q', '$rootScope', '$timeout', ($scope, $location, $http, $routeParams, $q, $rootScope, $timeout) ->
  $scope.currentDb = $routeParams.league_name
  $scope.lastSeq = null
  $scope.currentDb = null
  
  if $scope.$parent.canceler
    canceler = $scope.$parent.canceler
    $timeout(() ->
      canceler.resolve("League to follow changed")
    )
  
  $scope.currentLeague = () ->
    $routeParams.league_name
  
  $http.get("<%= couchdb_league_base %>"+$scope.currentLeague()+"<%= couchdb_league_ext %>").success((data) ->
    $scope.matches = {}
    $scope.updateMatches(data.rows)
    $scope.pollMatches($scope.currentLeague())
  )
     
  $scope.setDetails = (match) ->
    otherBets = $scope.$parent.filterKeys(match)
    $scope.details = {}
    $scope.detailsFor = match["_id"]
    for bet in otherBets
      $scope.details[bet] = match[bet]
  
  $scope.toggleDetails = (matchKey, match) ->
    if matchKey == $scope.detailsFor
      $scope.details = null
      $scope.detailsFor = null
    else
      $scope.setDetails(match)
      for key of match 
        if key not in ["_id", "_rev", "Full1X2"]
          $scope.selectedBetType = key
      
  $scope.selectBetType = (betType) ->
    $scope.selectedBetType = betType
      
  $scope.pollAddress = () ->
    if $scope.lastSeq
      "<%= couchdb_league_base %>"+$scope.currentLeague()+"<%= couchdb_match_changes_since %>" + $scope.lastSeq
    else
      "<%= couchdb_league_base %>"+$scope.currentLeague()+"<%= couchdb_match_changes %>"
  
  $scope.countMatches = () ->
    if $scope.matches
      Object.keys($scope.matches).length
    else
      0
      
  $scope.updateMatches = (results) ->
    n = new Date()
    for result in results
      start = new Date(result.doc._id.split("/")[0] + "-00:00")
      if start > n
        $scope.matches[result.doc["_id"]] = result.doc
        if result.doc["_id"] is $scope.detailsFor
          $scope.setDetails result.doc
      else if $scope.matches[result.doc["_id"]]?
        $scope.matches[result.doc["_id"]] = null
  
  $scope.pollMatches = (db_name) ->
    if $scope.currentLeague() == null || $scope.currentLeague() != db_name
      return
  
    $scope.$parent.canceler = $q.defer();

    $http.get($scope.pollAddress(), timeout: $scope.$parent.canceler.promise).success((data) ->
      if $scope.currentLeague() == db_name
        if $scope.lastSeq 
          if data.results
            $scope.updateMatches(data.results)
        if data.last_seq
          $scope.lastSeq = data.last_seq
        $scope.pollMatches(db_name)
    ).
    error((err) ->
      # check if aborted
    )
    
  $rootScope.$on("$locationChangeStart", (event, next, current) -> 
    if $scope.$parent && $scope.$parent.canceler
      canceler = $scope.$parent.canceler
      $timeout(() ->
        canceler.resolve("League to follow changed")
      )
  ) 
]
